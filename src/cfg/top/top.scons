import os


ip       = ['pll', 'pcie']

#-------------------------------------------------------------------------------
#
#    Configuration
#
cfg  = import_config('main.yml')
ext  = import_config('filetype.yml')
dirs = import_config('dirpath.yml')

ConfigName = cfg.NAME
DEVICE     = cfg.DEVICE

create_dirs(dirs.OUTPUT)

#-------------------------------------------------------------------------------
#
#    Environment
#
Import('envx')

envx.Append(CFG  = cfg)
envx.Append(EXT  = ext)
envx.Append(DIRS = dirs)

envx.Tool('vivado')
envx.Tool('questa')

print(envx['VOPT_FLAGS'])

envx['CFG_PATH']             = os.path.abspath(os.curdir)
envx['SETTINGS_SEARCH_PATH'] = [dirs.CFG_SETTINGS]
envx['IPSIM_CONFIG_PATH']    = dirs.SRC_IPSIM


envx.Append(SYNFLAGS = ' -notrace')
envx.Append(SYNFLAGS = ' -nojournal')

#-------------------------------------------------------------------------------
#
#    Targets
#
ipcs   = envx.IpCreateScripts(ip)
ipss   = envx.IpSynScripts(ip)
ip_xci = envx.CreateIps(ipcs)
ip_dcp = envx.SynIps(ipss, ip_xci)

ip_sls = envx.IpSimLibScripts(ip_xci)
simlib = envx.CompileSimlib(dirs.IP_SIMLIB, ip_sls)
                       
#-------------------------------------------------------------------------------
envx.Alias('ipcs',   ipcs)
envx.Alias('ipss',   ipss)
envx.Alias('ip_xci', ip_xci)
envx.Alias('ip_dcp', ip_dcp)
Default(ip_dcp)

envx.Alias('ip_sls', ip_sls)
envx.Alias('simlib', simlib)

#-------------------------------------------------------------------------------
#
#    Info
#
Help("""
    Configuration 'top' available targets:
    ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
        ipcs   : IP Create Scripts
        ipss   : IP Synthesize Scripts
        ip_xci : Create IPs
        ip_dcp : Create IP design checkpoints by sythisizing of 'xci's
********************************************************************************     
""")
#-------------------------------------------------------------------------------

