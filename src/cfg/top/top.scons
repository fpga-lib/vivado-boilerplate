import os

ip       = ['pll', 'pcie']

#-------------------------------------------------------------------------------
#
#    Configuration
#
cfg  = import_config('main.yml')
ext  = import_config('filetype.yml')
dirs = import_config('dirpath.yml')

DEVICE = cfg.DEVICE

#-------------------------------------------------------------------------------
#
#    Environment
#
Import('envx')

envx.Tool('vivado')
envx.Tool('questa')

envx['TOP_NAME']             = cfg.TOP_NAME
envx['TESTBENCH_NAME']       = cfg.TESTBENCH_NAME
envx['DEVICE']               = cfg.DEVICE


# path
envx['CFG_PATH']             = os.path.abspath(os.curdir)  # current configuration path
envx['SETTINGS_SEARCH_PATH'] = [dirs.SETTINGS]             # root dir for setting files (typically *.yml)
envx['IPSIM_SRC_CFG_PATH']   = dirs.IPSIM_SRC_CFG          # path to dir with IP sim sources list files
envx['IP_OOC_PATH']          = dirs.IP_OOC                 # path for out-of-context synthesized IP cores
envx['CFG_IP_PATH']          = dirs.CFG_IP                 # path for IP core configuration settings files

src     = read_sources('src_syn.yml')
src_sim = read_sources('src_sim.yml')

#print(src, src_sim)


#-------------------------------------------------------------------------------
#
#    Targets
#
IP_Create_Scripts = envx.IpCreateScripts(ip)
IP_Syn_Scripts    = envx.IpSynScripts(ip)
IP_Cores          = envx.CreateIps(IP_Create_Scripts)
IP_OOC_Syn        = envx.SynIps(IP_Syn_Scripts, IP_Cores)

IP_SimLib_Scripts = envx.IpSimLibScripts(IP_Cores)
IP_SimLib         = envx.CompileSimLib(IP_SimLib_Scripts)
                   
WLib              = envx.CompileWorkLib(src + src_sim)
    
#-------------------------------------------------------------------------------
#
#    Targets Aliases
#
envx.Alias('ipcs',     IP_Create_Scripts)
envx.Alias('ipss',     IP_Syn_Scripts)
envx.Alias('ip_cores', IP_Cores)
envx.Alias('ip_ooc',   IP_OOC_Syn)

envx.Alias('ip_sls', IP_SimLib_Scripts)
envx.Alias('simlib', IP_SimLib)

envx.Alias('wlib', WLib)

envx.AlwaysBuild(WLib)

Default(IP_SimLib)

#-------------------------------------------------------------------------------
#
#    Info
#
Help("""
    Configuration 'top' available targets:
    ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
        ipcs     : IP Create Scripts
        ipss     : IP Synthesize Scripts
        ip_cores : Create IPs
        ip_ooc   : Create IP design checkpoints by sythisizing of 'xci's
        ip_sls   : IP SimLib Scripts
        simlib   : IP SimLib (default)
********************************************************************************     
""")
#-------------------------------------------------------------------------------

